###################################################################
# Author: Jun Zhu <jun.zhu@xfel.eu>                               #
# Copyright (C) European X-Ray Free-Electron Laser Facility GmbH. #
# All rights reserved.                                            #
###################################################################

# extra-foam cmake module
# This module sets the following variables in your project::
#
#   extra-foam_FOUND - true if extra-foam found on the system
#   extra-foam_INCLUDE_DIR - the directory containing extra-foam headers
#   extra-foam_LIBRARY - empty

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(xtensor)
find_dependency(xtensor-blas)

if(NOT TARGET @PROJECT_NAME@)
  include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
  get_target_property(@PROJECT_NAME@_INCLUDE_DIR @PROJECT_NAME@ INTERFACE_INCLUDE_DIRECTORIES)
endif()

if(FOAM_USE_XSIMD)
    find_dependency(xsimd)
    target_link_libraries(@PROJECT_NAME@ INTERFACE xsimd)
    target_compile_definitions(@PROJECT_NAME@ INTERFACE FOAM_USE_XSIMD)
endif()

if(FOAM_USE_TBB)
    find_dependency(TBB)
    target_include_directories(@PROJECT_NAME@ INTERFACE ${TBB_INCLUDE_DIRS})
    target_link_libraries(@PROJECT_NAME@ INTERFACE ${TBB_LIBRARIES})
    target_compile_definitions(@PROJECT_NAME@ INTERFACE FOAM_USE_TBB)
endif()

set_and_check(extra-foam_INCLUDE_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_INCLUDEDIR@")
set(extra-foam_LIBRARY "")
check_required_components(extra_foam)
